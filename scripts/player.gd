extends CharacterBody2D

@onready var sprite: AnimatedSprite2D = $AnimatedSprite2D
@onready var camera := $Camera2D
@onready var health_bar: TextureProgressBar = $TextureProgressBar
@onready var weapon_manager: WeaponManager = $WeaponManager
@onready var animated_sprite: AnimatedSprite2D = $AnimatedSprite2D

var speed := 200.0
var max_health := 100
var health := max_health
var is_dead := false
var facing_direction := 1

func _ready():
	camera.make_current()
	health_bar.value = health
	health_bar.max_value = max_health
	var whip_data = load("res://data/whip_data.tres")
	$WeaponManager.add_weapon(whip_data)

func _physics_process(_delta):
	if is_dead:
		return
	var direction = Input.get_vector("move_left", "move_right", "move_up", "move_down")
	if direction.x != 0:
		facing_direction = sign(direction.x)
	velocity = direction * speed
	move_and_slide()
	update_animation(direction)

func update_animation(direction: Vector2):
	if direction == Vector2.ZERO:
		sprite.play("idle")
	else:
		if abs(direction.x) > abs(direction.y):
			if direction.x > 0:
				sprite.play("walk_right")
			else:
				sprite.play("walk_left")
		else:
			if direction.y > 0:
				sprite.play("walk_down")
			else:
				sprite.play("walk_up")

func take_damage(damage: int):
	if is_dead:
		return
	health -= damage
	health_bar.value = health
	if health <= 0:
		die()

func die():
	is_dead = true
	velocity = Vector2.ZERO
	sprite.play("die")
